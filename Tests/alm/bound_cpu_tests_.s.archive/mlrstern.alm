"  ******************************************************
"  *                                                    *
"  *                                                    *
"  * Copyright (c) 1972 by Massachusetts Institute of   *
"  * Technology and Honeywell Information Systems, Inc. *
"  *                                                    *
"  *                                                    *
"  ******************************************************

"  ******************************************************
"  *                                                    *
"  *                                                    *
"  *    Copyright, (C) Honeywell Information Systems    *
"  *    Inc., 1980.                                     *
"  *                                                    *
"  *                                                    *
"  ******************************************************

"This routine ckecks an MLR that failed when placing the fill character
"across a page boundary.

"If the MLR fails, a DRL fault is forced.
"Successful execution of the MLR is checked by using a CMPC AND a LDAQ/CMPAQ
"to verify that the CMPC worked correctly. If the CMPC and LDAQ/CMPAQ results
"agree, then the DRL fault is signaled if they sensed a failure. If they
"disagree then a MME4 is forced to denote that the CMPC failed. If both the MLR
"AND CMPC failed a MME1 is forced.


	name	mlrstern
	entry	mlrstern
	temp 	eis_misc
	temp	ou_misc
mlrstern:	push
	epplb	ap|2,*
	epplb	lb|0,*	"get ptr to test_data1_
	eppsb	ap|4,*
	eppsb	sb|0,*	"get ptr to test_cpu temp seg
	stz	eis_misc
	stz	ou_misc
	lda	=o235253,dl
	ldq	=o167773,dl
	eax7	6
	eax6	5

	mlr	(pr,rl,al),(pr,rl,ql),fill(040)
	desc9a	lb|-1(3),x6
	desc9a	sb|0,x7

	cmpc	(pr,rl,al),(pr,rl,ql),fill(040)
	desc9a	lb|-1(3),x6
	desc9a	sb|0,x7
	tnz	fail1
ou_check:
	ldaq	sb|15358
	cmpaq	constant1
	tnz	fail2
	ldaq	sb|15360
	cmpaq	constant2
	tnz	fail2
validate:
	lda	eis_misc
	cmpa	ou_misc
	tnz	eis_failed	"either the mlr or cmpc failed
	ldq	0,dl
	cmpq	eis_misc
	tnz	mlr_failed
	epbpsb	sp|0
	return

fail1:	aos	eis_misc		"remember cmpc miscompared
	tra	ou_check		"now validate with std instrs
fail2:	aos	ou_misc		"remember ldaq/cmpaq miscompared
	tra	validate

	even
constant1:
	oct	000000000165
	oct	166167170171
constant2:
	oct	040000000000
	oct	0
cmpc_failed:
	mme4

mlr_failed:
	drl

eis_failed:
	lda	ou_misc
	cmpa	0,dl
	tze	cmpc_failed
	mme1			"MLR and CMPC failed
	end
