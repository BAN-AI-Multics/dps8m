" ***********************************************************
" *                                                         *
" * Copyright, (C) Honeywell Information Systems Inc., 1982 *
" *                                                         *
" ***********************************************************
"
" Modified Nov 1983 by Rich Coppola to add test for another cmpc failure
" which is similar to the one originally covered by this test.
	

	name	cmpc_zero_ind
	entry	cmpc_zero_ind
	tempd	ARG
cmpc_zero_ind:
	push
	ldx3	100,du
	eppbp	ap|2,*	"get ptr to temp seg
	eppbp	bp|0,*
	adwpbp	17401,du
	mlr	(),(pr)
	desc9a	data,32
	desc9a	bp|0,32
	
	eppbb	ap|2,*
	eppbb	bb|0,*
	adwpbb	17073,du

	
loop:
	call 	test_cpu$deactivate_tempseg     "now deactivate it
	eppbp	data
	lda	l1
	ldq	l2
	lrl	36
	sblq	l2
	adlq	2,dl
	scd	(pr,rl,al),(pr)
	desc9a	bb|0,ql
	desc9a	bp|0
	arg	ARG
	ttn	scd_fail
	adla	1,dl
	adla	ARG
	sta	ARG
	ldq	l2

	call 	test_cpu$deactivate_tempseg     "now deactivate it again

	cmpc	(pr,rl,al),(pr,rl)
	desc9a	bb|-1(3),ql
	desc9a	bp|0,ql
	tze	flush_it
zop:	oct	0

flush_it:	call 	test_cpu$deactivate_tempseg     "now deactivate it again

	eax3	-1,3		"decrement loop count
	tnz	loop
				"Now start next test of cmpc
	ldx3	100,du
	eppbb	ap|2,*
	eppbb	bb|0,*
	eppbp	bb|0,*
	eppab	bb|0,*
	adwpbb	4402,du		"oct 10462
	adwpab	254480,du		"oct 761020
	lda	253,dl
	ldq	3921,dl

	mlr	(),(pr,rl),fill(040)
	desc9a	NUM,3
	desc9a	bb|0,al
	
	mlr	(),(pr,rl),fill(040)
	desc9a	NUM,3
	desc9a	ab|0,ql

cmpc_loop:
	call 	test_cpu$deactivate_tempseg     "now deactivate it
	even
	cmpc	(pr,rl),(pr,rl),fill(040)
	desc9a	bb|0,al
	desc9a	ab|0,ql
	tnz	ZOP
	eax3	-1,3			"decrement loop count
	tnz	cmpc_loop
	return

ZOP:	oct	0
scd_fail: mme4

NUM:	oct	061060061000

l1:	oct	000000031142
l2:	oct	000000000040
	even
data:	aci	"FORM_5                          "

	end

