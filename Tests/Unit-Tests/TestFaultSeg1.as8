

"""
""" Segment 1
"""

	macro	msg
	absa	#1
	emcall	21
	endm

main:
	msg	msg_main_entry

" Run the tests with out faulting

	msg	msg_without

	lda	=0,dl
	sta	mme_enable
	tsx2	run_tests

" Run the tests with faulting

	msg	msg_with

	lda	=1,dl
	sta	mme_enable
	tsx2	run_tests

	msg	msg_done
	emcall	18

	macro	unmap_memory
	lda	mme_enable
	mme
	endm

run_tests:

" Test 1: simple direct R,n

	msg	msg_test1
	equ	t1_data,123001001

	lda	=0,dl

	unmap_memory
	lda	t1_a1

	cmpa	t1_a1
	tze	t1_pass
	dis	*
t1_pass:
	msg	msg_test_ok

" Test 2: simple indirect

	msg	msg_test2

	equ	t2_data,234002001

	lda	=0,dl

	unmap_memory
	lda	t2_p1,*	"t2_p1 -> t2_a1

	cmpa	t2_a1
	tze	t2_pass
	dis	*
t2_pass:
	msg	msg_test_ok


" Test 3: double indirect

	msg	msg_test3

	equ	t3_data,345003001

	lda	=0,dl

	unmap_memory
	lda	t3_p1,*	"t3_p1 -> t3_p2 -> t3_a1

	cmpa	t3_a1
	tze	t3_pass
	dis	*
t3_pass:
	msg	msg_test_ok

" Test 4: simple indirect with indexing

	msg	msg_test4

	equ	t4_data,456004001

	lda	=0,dl
	ldx3	=1,du

	unmap_memory
	lda	t4_p1,3* " t4_p1[1] -> t4_a1

	cmpa	t4_a1
	tze	t4_pass
	dis	*
t4_pass:
	msg	msg_test_ok

" Test 5: indirect with ITS

	msg	msg_test5

	equ	t5_data,567005001

	lda	=0,dl

	unmap_memory
	lda	t5_p1,*	"t5_p1 -> seg2:t5_a1

	cmpa	t5_a1
	tze	t5_pass
	dis	*
t5_pass:
	msg	msg_test_ok

" Test 6: indirect with ITS indirect

	msg	msg_test6

	equ	t6_data,678006001

	lda	=0,dl

	unmap_memory
	lda	t6_p1,*	"t6_p1 -> seg2:t6_p2 -> t6_a1

	cmpa	t6_a1
	tze	t6_pass
	dis	*
t6_pass:
	msg	msg_test_ok

"
" done with tests
"

	tra	0,2

mme_enable:
	oct	0

msg_test1:
	aci	'Test 1 R,n ...\n\0\0'
	oct	0

msg_test_ok:
	aci	'... ok\n\0\0'
	oct	0

msg_test2:
	aci	'Test 2 R,* ...\n\0\0'
	oct	0

msg_test3:
	aci	'Test 3 R,*,* ...\n\0\0'
	oct	0

msg_test4:
	aci	'Test 4 R,3* ...\n\0\0'
	oct	0

msg_test5:
	aci	'Test 5 R,*seg ...\n\0\0'
	oct	0

msg_test6:
	aci	'Test 6 R,*seg, * ...\n\0\0'
	oct	0

msg_main_entry:
	aci	'Fault test main\n\0\0'
	oct	0
msg_dbg_mme_back:
	aci	'back from mme\n\0\0'
	oct	0
msg_done:
	aci	'Fault test done\n\0\0'
	oct	0
msg_without:
	aci	'Running tests without page faulting\n\0\0'
	oct	0
msg_with:
	aci	'Running tests with page faulting\n\0\0'
	oct	0

" Page 1: indirect words

	org	1*1024

" Test 2

t2_p1:	arg	t2_a1

" Test 3

t3_p1:	arg	t3_p2,*

" Test 4

t4_p1:	dec	-1
	arg	t4_a1

" Test 5

	even
t5_p1:	its	2,1024	" t5_a1 is at offset 1024 in seg 2.

" Test 6

	even
t6_p1:	its	2,0,*	" t6_p2 is at offset 0 in seg 2.

" Page 2: double indirect words

org	2*1024

" Test 3

t3_p2:	arg	t3_a1

" Page 3: data

	org	3*1024

" Test 1

t1_a1:	dec	t1_data

" Test 2

t2_a1:	dec	t2_data

" Test 3

t3_a1:	dec	t3_data

" Test 4

t4_a1:	dec	t4_data

" Test 5

t5_a1:	dec	t5_data	" This is not actually the data read by test 5 -- it
			" is over on seg 2.

" Test 6

t6_a1:	dec	t6_data	" This is not actually the data read by test 6 -- it
			" is over on seg 2.

